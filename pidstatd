#!/usr/local/bin/perl -Tw
# pidstatd - distributed lock handler for perl IPC::Locker
# $Id: pidstatd,v 1.2 2002/07/28 20:13:32 wsnyder Exp $
# Wilson Snyder <wsnyder@wsnyder.org>
################ Introduction ################
#
# This program is Copyright 2001 by Wilson Snyder.
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of either the GNU General Public License or the
# Perl Artistic License, with the exception that it cannot be placed
# on a CD-ROM or similar media for commercial distribution without the
# prior approval of the author.
# 
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
# 
# If you do not have a copy of the GNU General Public License write to
# the Free Software Foundation, Inc., 675 Mass Ave, Cambridge, 
# MA 02139, USA.
######################################################################

require 5.004;
use lib './blib/lib';	# testing
use English;
use Getopt::Long;
use Pod::Text;
use IPC::PidStat::PidServer;

BEGIN { $ENV{PATH} = '/usr/ucb:/bin' }	# Secure path

######################################################################
# configuration

######################################################################
# globals

use vars qw($Pid);

######################################################################
# main

my $Debug = 0;
my %server_params = ();

if (!GetOptions (
		 "help"		=> \&usage,
		 "debug"	=> \&debug,
		 "version"	=> \&version,
		 "port=i"	=> sub {$server_params{port} = $_[1];},
		 )) {
    &usage(); }

# Loop in case something kills us
$SIG{HUP}  = \&sig_HUP;
$SIG{CHLD} = \&sig_HUP;
while (1) {
    print "Starting server\n" if $Debug;
    unless ($Pid = fork) {
        IPC::PidStat::PidServer->new(%server_params)->start_server ();
	exit(0);
    }
    waitpid($Pid,0) if $Pid;
    warn "%Warning: Server aborted\n" if $Debug;
    sleep(1);
    kill 9, $Pid if $Pid;
    $Pid = 0;
    sleep(1);
}

exit (0);

sub sig_HUP {
    kill 9, $Pid if $Pid;
    $Pid = 0;
}

######################################################################

sub usage {
    print '$Id: pidstatd,v 1.2 2002/07/28 20:13:32 wsnyder Exp $ ', "\n";
    $SIG{__WARN__} = sub{};	#pod2text isn't clean.
    pod2text($0);
    exit(1);
}

sub version {
    print 'Version: $Id: pidstatd,v 1.2 2002/07/28 20:13:32 wsnyder Exp $ ';
    print "\n";
    exit (1);
}

sub debug {
    $Debug = 1;
    $IPC::PidStat::PidServer::Debug = 1;
}

######################################################################
__END__

=pod

=head1 NAME

pidstatd - Determine if process ID is running for perl IPC::Locker

=head1 SYNOPSIS

B<pidstatd>
[ B<--help> ]
[ B<--port=>I<port> ]
[ B<--version> ]

=head1 DESCRIPTION

Pidstatd will start a UDP daemon.  The daemon responds to requests that
contain a PID with a packet indicating the PID and if the PID currently
exists.

The Perl IPC::Locker package optionally uses this daemon to break locks
for PIDs that no longer exists.

=head1 ARGUMENTS

=over 4

=item --help
Displays this message and program version and exits.

=item --port
Specifies the port number to be used.

=item --version
Displays program version and exits.

=back

=head1 SEE ALSO

C<IPC::Locker>, C<lockerd>

=head1 AUTHORS

Wilson Snyder <wsnyder@wsnyder.org>

=cut
######################################################################
